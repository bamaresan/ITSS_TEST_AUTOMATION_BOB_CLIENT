*********************************************************************
*-----------------------------------------------------------------------------
* <Rating>-50</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE TAUT.OFS.UPLOAD.TARGET.LOAD

* 20160927 - Redisgned to Multithreaded program to resolve Transaction boundary issue - Sunder
*2016-10-04 New condition added to restrict the execution when the parameterised error count exceeds.
    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INCLUDE TAUT.BP I_F.TAUT.PARAMETER
    $INCLUDE TAUT.BP I_F.TAUT.APPL.DETAILS
    $INCLUDE TAUT.BP I_TAUT.OFS.UPLOAD.TARGET.COMMON

    GOSUB OPEN.FILES
    GOSUB INITIALISE

    RETURN

OPEN.FILES:
***********
    FN.STANDARD.SELECTION = 'F.STANDARD.SELECTION'
    F.STANDARD.SELECTION = ''
    CALL OPF(FN.STANDARD.SELECTION,F.STANDARD.SELECTION)

    FN.TAUT.PARAM = 'F.TAUT.PARAMETER'
    F.TAUT.PARAM = ''
    CALL OPF(FN.TAUT.PARAM,F.TAUT.PARAM)

    FN.TAUT.APP.DET = 'F.TAUT.APPL.DETAILS'
    F.TAUT.APP.DET = ''
    CALL OPF(FN.TAUT.APP.DET,F.TAUT.APP.DET)

    FN.TAUT.APPL.CHANGE = 'F.TAUT.APPL.CHANGE'
    F.TAUT.APPL.CHANGE = ''
    CALL OPF(FN.TAUT.APPL.CHANGE,F.TAUT.APPL.CHANGE)

    FN.TAUT.W.NEW.ID.REF = 'F.TAUT.W.NEW.ID.REF'
    F.TAUT.W.NEW.ID.REF = ''
    CALL OPF(FN.TAUT.W.NEW.ID.REF, F.TAUT.W.NEW.ID.REF)

    FN.OFS.SOURCE = 'F.OFS.SOURCE'
    F.OFS.SOURCE = ''
    CALL OPF(FN.OFS.SOURCE,F.OFS.SOURCE)

    OFS.SRC.ID = "TAUT.OFS.UPLOAD"
    CALL F.READ(FN.OFS.SOURCE,OFS.SRC.ID,R.OFS.SOURCE,F.OFS.SOURCE,'')


    R.TAUT.APPL.CHANGE.SYS = ''
    CHG.READ.ERR = ''
    CALL F.READ(FN.TAUT.APPL.CHANGE,'SYSTEM',R.TAUT.APPL.CHANGE.SYS,F.TAUT.APPL.CHANGE,CHG.READ.ERR)

    R.TAUT.PARAM = ''
    TAUT.PARAM.READ.ERR = ''
    CALL F.READ(FN.TAUT.PARAM,'SYSTEM',R.TAUT.PARAM,F.TAUT.PARAM,TAUT.PARAM.READ.ERR)

    R.TAUT.APP.DET = ''
    TAUT.APP.DET.READ.ERR = ''
    CALL F.READ(FN.TAUT.APP.DET,'SYSTEM',R.TAUT.APP.DET,F.TAUT.APP.DET,TAUT.APP.DET.READ.ERR)

    FN.AA.PROPERTY = 'F.AA.PROPERTY'
    F.AA.PROPERTY = ''
    CALL OPF(FN.AA.PROPERTY, F.AA.PROPERTY)
    RETURN

INITIALISE:
************
    Y.ERROR.CNT = 1
    IF R.TAUT.PARAM THEN
        GOSUB OPEN.TAUT.LOG.FILE
        GOSUB OPEN.TAUT.OFS.QUEUE

        TRANS.TYPE = R.TAUT.PARAM<TAUT.TRANS.TYPE>
        EXCL.FUNCTION = R.TAUT.PARAM<TAUT.EXCL.FUNCTION>
        EXCL.VERSION = R.TAUT.PARAM<TAUT.EXCL.VERSION>
        EXCL.APPL = R.TAUT.PARAM<TAUT.EXCL.APPL>
        EXCL.USER = R.TAUT.PARAM<TAUT.EXCL.USER>
        EXCL.COMPANY = R.TAUT.PARAM<TAUT.EXCL.COMPANY>
        TGT.T24.RELEASE = R.TAUT.PARAM<TAUT.TGT.T24.REL>
        SAMPLE.CNTR = R.TAUT.PARAM<TAUT.SAMPLE.CNTR>
        PRD.ONLINE.JNL = R.TAUT.PARAM<TAUT.PRD.ONLINE.JNL>
        JNL.FREQ = R.TAUT.PARAM<TAUT.ONLINE.JNL.FREQ>
        OFS.USER = R.TAUT.PARAM<TAUT.TGT.OFS.USER>
        OFS.PASSWD = R.TAUT.PARAM<TAUT.TGT.OFS.PASSWD>
        START.JNL = R.TAUT.PARAM<TAUT.START.JNL>
        END.JNL = R.TAUT.PARAM<TAUT.END.JNL>
        OFS.CONV.RTN = R.TAUT.PARAM<TAUT.OFS.CONV.RTN>
        UPDATE.RESP = R.TAUT.PARAM<TAUT.UPDATE.RESP>
*2016-10-04 begin
        Y.ALLOWED.ERR = R.TAUT.PARAM<TAUT.TGT.UPL.ABORT.CNT>
*2016-10-04 end
    END

    IF R.TAUT.APP.DET THEN
        SPE.FILE.NAME = R.TAUT.APP.DET<TAUT.FILE.NAME>
        OFS.PRE.CONV.RTN = R.TAUT.APP.DET<TAUT.PRE.OFS.RTN>
        OFS.POST.CONV.RTN = R.TAUT.APP.DET<TAUT.POST.OFS.RTN>
        MESSAGE.SYNTAX = R.TAUT.APP.DET<TAUT.MESSAGE.SYNTAX>
        DELIM = R.TAUT.APP.DET<TAUT.MSG.DELIMITER>
        POST.MESSAGE = R.TAUT.APP.DET<TAUT.POST.MESSAGE>
    END

    Y.DELIM = ","
    R.TAUT.UPLD.DONE.RPT = ""
    R.FAILED.REPORT = ""
    RETURN

OPEN.TAUT.LOG.FILE:
*******************
    T.LOG.LEVEL = R.TAUT.PARAM<TAUT.LOG.LEVEL>

    IF T.LOG.LEVEL NE 'NONE' THEN
        LOG.FILE = R.TAUT.PARAM<TAUT.LOG.FILE>

        F.TAUT.LOG = ''
        OPEN LOG.FILE TO F.TAUT.LOG ELSE
            LOG.LEVEL = 1     ;* Fatal
            TAUT.TEXT = 'Cannot open the LOG file: ':LOG.FILE
            GOSUB UPDATE.TAUT.LOG
            RETURN
        END
    END

    IF TAUT.PARAM.READ.ERR OR R.TAUT.PARAM EQ '' THEN
        LOG.LEVEL = 1         ;* Fatal
        TAUT.TEXT = 'TAUT.PARAMETER RECORD MISSING'
        GOSUB UPDATE.TAUT.LOG
        RETURN
    END

    IF TAUT.APP.DET.READ.ERR OR R.TAUT.APP.DET EQ '' THEN
        LOG.LEVEL = 3         ;* Info
        TAUT.TEXT = 'TAUT.APPL.DETAILS RECORD MISSING'
        GOSUB UPDATE.TAUT.LOG
    END

    RETURN

OPEN.TAUT.OFS.QUEUE:
********************
    DATA.SYNC = R.TAUT.PARAM<TAUT.DATA.SYNC>
    IF DATA.SYNC EQ 'OFFLINE' THEN
        STORE.FWD.QUEUE = R.TAUT.PARAM<TAUT.STORE.TXNS.OFS>
        F.STORE.FWD.QUEUE = ''
        CALL OPF(STORE.FWD.QUEUE,F.STORE.FWD.QUEUE)

        FN.TAUT.STORE.JOURNAL = R.TAUT.PARAM<TAUT.PROCESSED.JNL>
        F.TAUT.STORE.JOURNAL  = ''
        CALL OPF(FN.TAUT.STORE.JOURNAL,F.TAUT.STORE.JOURNAL)

        FN.TGT.UPL.REPEAT = R.TAUT.PARAM<TAUT.TGT.UPL.REPEAT>
        F.TGT.UPL.REPEAT = ""
        CALL OPF(FN.TGT.UPL.REPEAT,F.TGT.UPL.REPEAT)

        FN.TGT.UPL.FAILED = R.TAUT.PARAM<TAUT.TGT.UPL.FAILED>
        F.TGT.UPL.FAILED  = ""
        CALL OPF(FN.TGT.UPL.FAILED,F.TGT.UPL.FAILED)

        FN.TGT.UPLOAD.DONE = R.TAUT.PARAM<TAUT.TGT.UPLOAD.DONE>
        F.TGT.UPLOAD.DONE = ""
        CALL OPF(FN.TGT.UPLOAD.DONE,F.TGT.UPLOAD.DONE)

        FN.TGT.UPL.REPORTS= R.TAUT.PARAM<TAUT.TGT.UPL.REPORTS>
        F.TGT.UPL.REPORTS = ""
        CALL OPF(FN.TGT.UPL.REPORTS,F.TGT.UPL.REPORTS)

        Y.TIME.DATE = TIMEDATE()
        CONVERT ":" TO "-" IN Y.TIME.DATE

        Y.TGT.UPL.FAILED.REPORT.ID = Y.TIME.DATE:"_Failed upload transaction.csv"
        Y.TGT.UPL.DONE.RPT.ID = Y.TIME.DATE:"_Processed transaction.csv"

    END
    SOURCE.FILE = R.TAUT.PARAM<TAUT.SOURCE.FILE>
    RETURN

UPDATE.TAUT.LOG:
****************
    YID = ''
    CALL ALLOCATE.UNIQUE.TIME(YID)
    IF TAUT.TEXT THEN
        IF T.LOG.LEVEL EQ 'FULL' THEN
            BEGIN CASE
            CASE LOG.LEVEL = 1
                TAUT.TEXT = "Critical Error :" :TIMEDATE():" ":TAUT.TEXT
            CASE LOG.LEVEL = 2
                TAUT.TEXT = "Warning  :" :TIMEDATE():" ":TAUT.TEXT
            CASE LOG.LEVEL = 3
                TAUT.TEXT = "Info :" :TIMEDATE():" ":TAUT.TEXT
            END CASE

            WRITE TAUT.TEXT TO F.TAUT.LOG,YID
        END

        IF T.LOG.LEVEL EQ 'FATAL' AND LOG.LEVEL EQ 1 THEN
            TAUT.TEXT = "Critical Error :" :TIMEDATE():" ":TAUT.TEXT
            WRITE TAUT.TEXT TO F.TAUT.LOG,YID
        END
    END

    TAUT.TEXT = ''
    RETURN
END



